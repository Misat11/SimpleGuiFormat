/*
	Always use gradle shadow
*/

if (project.hasProperty("screamingRepository")) {
	defaultTasks 'clean', 'shadow', 'publishToMavenLocal', 'publish'
} else {
	defaultTasks 'clean', 'shadow', 'publishToMavenLocal'
}



import org.apache.tools.ant.filters.*
import org.w3c.dom.NodeList;
import org.w3c.dom.Element;


allprojects {
    group = 'org.screamingsandals.simpleinventories'
    version = '1.0.0'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

subprojects {

	apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        mavenLocal()
        
        maven {
            url = uri('https://papermc.io/repo/repository/maven-public/')
        }

        maven {
            url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/')
        }
    }

	shadowJar {
	   relocate 'com.univocity', 'org.screamingsandals.simpleinventories.univocity'
	}
	
	publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                pom.withXml {
                	NodeList list = asElement().getElementsByTagName("dependency")
                	for (int i = 0; i < list.getLength(); i++) {
                		((Element) list.item(i)).getElementsByTagName('scope').item(0).setTextContent("provided")
                	}
                }
            }
	    }
        
        if (project.hasProperty("screamingRepository")) {
	       repositories {
              maven {
              	  url = project.getProperty("screamingRepository") 
	          }
	      	}   

        }
    }
    
	processResources {
	    filter ReplaceTokens, tokens: [
	        "version": project.property("version")
	    ]
	}

    sourceCompatibility = '1.8'
}
